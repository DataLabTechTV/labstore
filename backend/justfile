set shell := ["bash", "-uc"]

set dotenv-load
set dotenv-required
set dotenv-path := "../.env"

benchmark_dir := "benchmarks/warp/"

benchmark_bucket := "warp-benchmark-bucket"
sandbox_bucket := "sandbox"

ls_host := env("LS_HOST")
ls_port := env("LS_PORT")
ls_admin_access_key := env("LS_ADMIN_ACCESS_KEY")
ls_admin_secret_key := env("LS_ADMIN_SECRET_KEY")

default:
    just -l

create-bucket bucket:
    #!/bin/bash
    export MC_HOST_local="http://{{ls_admin_access_key}}:{{ls_admin_secret_key}}@{{ls_host}}:{{ls_port}}"
    mc mb -p local/{{bucket}}

benchmark-bucket:
    just create-bucket {{benchmark_bucket}}

benchmark: benchmark-bucket
    #!/bin/bash
    cd {{benchmark_dir}}
    mkdir -p output/
    cd output/
    warp run ../config.yml

sandbox-bucket:
    just create-bucket {{sandbox_bucket}}

test-mc: sandbox-bucket
    #!/bin/bash

    export MC_HOST_local="http://{{ls_admin_access_key}}:{{ls_admin_secret_key}}@{{ls_host}}:{{ls_port}}"

    tmpfile=$(mktemp)

    echo "==> Generating temporary file with 10 MiB: $tmpfile"
    dd if=/dev/urandom of=$tmpfile bs=1M count=10

    echo "==> Sending temporary file: $tmpfile"
    mc --debug cp --disable-multipart $tmpfile local/{{sandbox_bucket}}/$(basename $tmpfile).mc

    echo "==> Cleaning up temporary file: $tmpfile"
    rm -f $tmpfile

test-rclone: sandbox-bucket
    #!/bin/bash

    tmpfile=$(mktemp)

    echo "==> Generating temporary file with 10 MiB: $tmpfile"
    dd if=/dev/urandom of=$tmpfile bs=1M count=10

    tmpconfig=$(mktemp)

    echo "==> Creating temporary rclone config: $tmpconfig"
    cat <<EOF >$tmpconfig
    [labstore]
    type = s3
    provider = Other
    access_key_id = {{ls_admin_access_key}}
    secret_access_key = {{ls_admin_secret_key}}
    endpoint = http://{{ls_host}}:{{ls_port}}
    acl = private
    EOF

    echo "==> Sending temporary file: $tmpfile"
    rclone copyto $tmpfile labstore:/{{sandbox_bucket}}/$(basename $tmpfile).rclone --config $tmpconfig

    echo "==> Cleaning up temporary files: $tmpfile, $tmpconfig"
    rm -f $tmpfile $tmpconfig

test-s5cmd: sandbox-bucket
    #!/bin/bash

    endpoint_url="http://{{ls_admin_access_key}}:{{ls_admin_secret_key}}@{{ls_host}}:{{ls_port}}"

    export AWS_ACCESS_KEY_ID={{ls_admin_access_key}}
    export AWS_SECRET_ACCESS_KEY={{ls_admin_secret_key}}

    tmpfile=$(mktemp)

    echo "==> Generating temporary file with 10 MiB: $tmpfile"
    dd if=/dev/urandom of=$tmpfile bs=1M count=10

    echo "==> Sending temporary file: $tmpfile"
    s5cmd --endpoint-url $endpoint_url cp $tmpfile s3://{{sandbox_bucket}}/$(basename $tmpfile).s5cmd

    echo "==> Cleaning up temporary file: $tmpfile"
    rm -f $tmpfile

clean:
    rm -rf {{join(benchmark_dir, "output")}}
